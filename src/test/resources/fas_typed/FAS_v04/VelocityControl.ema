package fas.demo_fas_Fkt_m;

import fas.basicLibrary.*;

component VelocityControl {
	port
		in N(0:255) BrakeForceBoosted_pc,
		in (-1820.39m/s:0.28m/s:1820.39m/s) V_CC_delta_kmh,
		in (0m/s:0.28m/s:1820.39m/s) VMax_kmh,
		in N(0:255) Accelaration_pedal_pc,
		in (0m/s:0.28m/s:1820.39m/s) V_Vehicle_kmh,
		in N(0:255) BrakeForce_pedal_pc,
		in N(0:255) BrakeForce_Distronic_pc,
		in N(0:255) Decelerator_pc,
		in N(0:255) BrakeForce_Emergency_pc,
		out N(0:255) Accelaration_pc,
		out N(0:255) BrakeForce_pc;

	instance Saturation<(-1820.39m/s:0.28m/s:1820.39m/s)> sat1, sat2;
	instance LookUpM<(0m/s:0.28m/s:1820.39m/s),N(0:100)> look1,look2;
	instance Max<N(0:255)> max1;
	instance Max<N(0:255),5> max2;
	instance SwitchB<N(0:255)> switch1;
	instance SwitchM<N(0:255)> switch2, switch3;
	instance Greater<(0m/s:0.28m/s:1820.39m/s)> greater1, greater2;
	instance And and1;
	instance MultDiv<N(0:255)> mul;

	connect Decelerator_pc -> mul.in1;
	connect V_Vehicle_kmh -> greater1.in1;
	connect VMax_kmh -> greater1.in2, greater2.in1;
	connect Accelaration_pedal_pc -> max1.in1;
	connect V_CC_delta_kmh -> sat1.in1, sat2.in1;
	connect BrakeForceBoosted_pc -> max2.in2;
	connect BrakeForce_pedal_pc -> max2.in3;
	connect BrakeForce_Distronic_pc -> max2.in4;
	connect BrakeForce_Emergency_pc -> max2.in5;
	connect greater1.out1 -> and1.in1;
	connect greater2.out1 -> and1.in2;
	connect 0 -> greater2.in2;
	connect sat1.out1 -> look1.in1;
	connect sat2.out1 -> look2.in1;
	connect look1.out1 -> max1.in2;
	connect look2.out1 -> switch2.in3;
	connect max1.out1 -> switch1.in3, switch2.cond;
	connect 0 -> switch2.in1;
	connect switch2.out1 -> max2.in1;
	connect and1.out1 -> switch1.cond;
	connect 0 -> switch1.in1;
	connect switch1.out1 -> mul.in2;
	connect 100 -> mul.in3;
	connect max2.out1 -> switch3.cond, BrakeForce_pc;
	connect mul.out1 -> switch3.in3;
	connect 0 -> switch3.in1;
	connect switch3.out1 -> Accelaration_pc;
}
