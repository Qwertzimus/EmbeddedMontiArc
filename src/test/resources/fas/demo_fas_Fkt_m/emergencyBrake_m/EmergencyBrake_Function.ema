package fas.demo_fas_Fkt_m.emergencyBrake_m;

import fas.basicLibrary.*;

component EmergencyBrake_Function {
	port
		in (0m/s:0.28m/s:1820.39m/s) V_Vehicle_ms,
		in (0m:200m) Distance_Object_m,
		in (-1820.39m/s:0.28m/s:1820.39m/s) V_Obj_rel_ms,
		out Z(0:255) BrakeForce_Emergency_pc,
		out B Acusti_warn_b;

	instance Saturation<(1m/s:0.28m/s:1820.39m/s)> sat1, sat2, sat3;
	instance Division<(0m/s:0.28m/s:1820.39m/s)> div1, div2;
	instance MinusPlus<(0m/s:0.28m/s:1820.39m/s)> minPlus1, minPlus2;
	instance Smaller<(0m/s:0.28m/s:1820.39m/s)> smaller1, smaller2;
	instance SwitchB<Z(0:40:80)> switch1, switch2;
	instance Max<Z(0:40:80)> max1;

	connect Distance_Object_m -> div1.in1;
	connect V_Obj_rel_ms -> sat1.in1;
	connect sat1.out1 -> div1.in2;
	connect V_Vehicle_ms -> div2.in1;
	connect 8/*demo_fas_Neg_Max_ms*/ -> div2.in2;
	connect div1.out1 -> minPlus1.in2, minPlus2.in2;
	connect 1/*demo_fas_Emerg_Brake_Time1*/ -> minPlus1.in1;
	connect 1.5/*demo_fas_Emerg_Brake_Time2*/ -> minPlus2.in1;
	connect minPlus1.out1 -> sat2.in1;
	connect sat2.out1 -> smaller1.in1;
	connect minPlus2.out1 -> sat3.in1;
	connect sat3.out1 -> smaller2.in1;
	connect div2.out1 -> smaller1.in2, smaller2.in2;
	connect smaller1.out1 -> switch1.cond, Acusti_warn_b;
	connect smaller2.out1 -> switch2.cond;
	connect 40/*demo_fas_Emerg_Brake_Force1*/ -> switch1.in1;
	connect 80/*demo_fas_Emerg_Brake_Force2*/ -> switch2.in1;
	connect 0 -> switch1.in3;
	connect 0 -> switch2.in3;
	connect switch1.out1 -> max1.in1;
	connect switch2.out1 -> max1.in2;
	connect max1.out1 -> BrakeForce_Emergency_pc;

}
